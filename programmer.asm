; TODO INSERT CONFIG CODE HERE USING CONFIG BITS GENERATOR
;      
;   PIC FLASH PROGRAMMER
;   
;            PIC	    AT29C256
;   ------------------------------------
;	PORTA[0-7]   --->      A[0-7]
;	PORTB[0-6]   --->      A[8-14]
;	PORTD[0-7]   --->      D[0-7]
;	RE0	     --->      /WE
;	RE1	     --->      /OE
;	RE2	     --->      /CE
    
#include "p16f917.inc"

; CONFIG
; __config 0x3CF4
 __CONFIG _FOSC_INTOSCIO & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_ON & _FCMEN_ON
    
;VARS
CRX EQU 0X24
TMPA EQU 0X25
TMPB EQU 0X26
PDATA EQU 0X27
    
RES_VECT  CODE    0x0000            ; processor reset vector
    GOTO    START                   ; go to beginning of program

; TODO ADD INTERRUPTS HERE IF USED

MAIN_PROG CODE                      ; let linker place main program

START
    BSF STATUS,RP0	;ENTER PAGE1
    
    BSF OSCCON,IRCF0 ; SET 8MHZ
    
    BCF TRISE,RE0
    BCF TRISE,RE1
    BCF TRISE,RE2
    
    BCF STATUS,RP0	;LEAVE PAGE1
    
    BSF PORTE,RE0
    BSF PORTE,RE1
    BSF PORTE,RE2
    
    BSF STATUS,RP0	;ENTER PAGE1
    
    CLRF TRISA
    CLRF TRISB
    CLRF ANSEL
    
    BSF CMCON0,CM0
    BSF CMCON0,CM1
    BSF CMCON0,CM2

    BSF TXSTA,BRGH ;SET HIGH SPEED UART
    BSF TXSTA,TXEN ;SET TX ENABLE
    
    CLRF SPBRG ; MAXIMUM BAUD RATE (8MHZ -> 500KBAUD)
    
    BCF STATUS,RP0	;LEAVE PAGE1
    
    BSF RCSTA,SPEN ;ENABLE AUSART, SET RX PIN INPUT
    BSF RCSTA,CREN ;ENABLE RX
    
    BSF STATUS,RP1	;ENTER PAGE2
    CLRF LCDCON
    BCF STATUS,RP1	;LEAVE PAGE2

WAITCMD
    BTFSS PIR1,RCIF ;TEST TO SEE IF WE HAVE A NEW CHAR ON THE ON THE RECEIVE BUFFER OR JUST A LEFTOVER ONE
    GOTO WAITCMD
    
    MOVF RCREG,W
    MOVWF CRX

    SUBLW 0X57
    BTFSC STATUS,Z
    CALL WRITE
    
    MOVF CRX,W
    SUBLW 0X52
    BTFSC STATUS,Z
    CALL READ
    
    GOTO WAITCMD   ;CONTINUOUSLY WAIT FOR AN 'R' FOR READ OR A 'W' FOR WRITE

WAITTX
    BTFSS PIR1,TXIF 
    GOTO WAITTX
    RETURN
    
WRITE
    MOVLW 0X41	 ;-|
    MOVWF TXREG	 ;-|- SEND 'A' FOR ACK
    CALL WAITTX
    
    BSF STATUS,RP0
    CLRF TRISD	    ;PORTD OUTPUT FOR WRITING TO THE ADDRESS BUS
    BCF STATUS,RP0

;	   DISABLE WRITE PROTECTION:
;-------------LOAD AA TO 5555-----------------
    MOVLW 0X55
    MOVWF PORTA
    MOVWF PORTB
    
    MOVLW 0XAA
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;---------------------------------------------

;------------LOAD 55 TO 2AAA------------------
    MOVWF PORTA  ;DEJA AVEM AA IN W
    MOVLW 0X2A
    MOVWF PORTB
    
    MOVLW 0X55
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;---------------------------------------------   

;------------LOAD 80 TO 5555------------------
    MOVWF PORTA
    MOVWF PORTB	    ;DEJA AVEM 55 IN W
    
    MOVLW 0X80
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;--------------------------------------------
    
;-----------LOAD AA TO 5555------------------
    ;DEJA AVEM 5555 IN AB
    
    MOVLW 0XAA
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;--------------------------------------------
    
;----------LOAD 55 TO 2AAA-------------------
    MOVWF PORTA ;DEJA AVEM AA IN W
    MOVLW 0X2A
    MOVWF PORTB
    
    MOVLW 0X55
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;-------------------------------------------
    
;-----------LOAD 20 TO 5555-----------------
    MOVWF PORTA
    MOVWF PORTB	 ;DEJA AVEM 55 IN W
    
    MOVLW 0X20
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;--------------------------------------------
    
    CLRF PORTA
    CLRF PORTB
    CLRF PORTD
    CLRF TMPA
    CLRF TMPB


BYTELOAD    
;    BTFSS PIR1,RCIF
;    GOTO BYTELOAD
    
    MOVF RCREG,W    ;-|
    MOVWF PORTD	    ;-|- TAKES THE BYTE FROM THE UART RECEIVE REGISTER AND PUTS THEM ON THE DATA BUS 
    MOVWF PDATA
    
    BCF PORTE,RE0   ;WRITE ENABLE ON	    ;-|
					    ;-|
    BCF PORTE,RE2   ;CHIP ENABLE ON	    ;-|
    NOP					    ;-|- SIGNALS CORRESPONDIG TO THE 'CE CONTROLLED' TIMING DIAGRAM FROM THE DATASHEET
    BSF PORTE,RE2   ;CHIP ENABLE OFF	    ;-|	 
					    ;-|	 
    BSF PORTE,RE0   ;WRITE ENABLE OFF	    ;-| 
    
    INCFSZ TMPA,F
    GOTO SKIPOVFW
    INCF TMPB,F
SKIPOVFW
    MOVF TMPA,W
    MOVWF PORTA
    MOVF TMPB,W
    MOVWF PORTB
    
    MOVLW 0X3F	    ;WE TEST FOR MULTIPLES OF 64 TO SIGNAL THAT WE FINISHED A PAGE
    ANDWF TMPA,W
    BTFSS STATUS,Z
    GOTO CONTBLOAD
    GOTO POLL
    
CONTBLOAD
    MOVLW 0X57	    ;-|
    MOVWF TXREG	    ;-|- SEND A 'W' TO SIGNAL THAT THE BYTE HAS BEEN WRITTEN
    CALL WAITTX
    GOTO BYTELOAD
    
POLL
    MOVLW 0X47	    ;SIGNAL TO THE COMPUTER THAT WE FINISHED THE PAGE
    MOVWF TXREG
    CALL WAITTX
    
    BSF STATUS,RP0
    MOVLW 0XFF
    MOVWF TRISD	    ;PORTD INPUT FOR POLLING
    BCF STATUS,RP0
    CLRF PORTD
    
;    MOVF TMPA,F
;    BTFSC STATUS,Z
;    DECF TMPB,F
;    DECF TMPA,F
;    MOVF TMPA,W		;POLLING V3 DEBUGGING
;    MOVWF PORTA
;    MOVF TMPB,W
;    MOVWF PORTB
    
    MOVF TMPB,W
    MOVF TMPA,F
    BTFSC STATUS,Z	;POLLING V2
    DECF TMPB,W
    MOVWF PORTB
    DECF TMPA,W
    MOVWF PORTA
    
    
;    MOVLW 0X01
;    SUBWF TMPA,W	;POLLINGUL AT THE PREVIOUS ADDRESS
;    MOVWF PORTA
;    BTFSC STATUS,C	  ;POLLING V1
;    GOTO POLLING
;    DECF TMPB,W
;    MOVWF PORTB
    
    
POLLING
    BCF PORTE,RE2
    BCF PORTE,RE1
    MOVF PORTD,W
    BSF PORTE,RE1
    BSF PORTE,RE2
    SUBWF PDATA,W
    BTFSS STATUS,Z
    GOTO POLLING
    
    MOVF TMPA,W
    MOVWF PORTA
    MOVF TMPB,W
    MOVWF PORTB
    
    BSF STATUS,RP0
    CLRF TRISD	    ;PORTD OUTPUT
    BCF STATUS,RP0
    
    MOVLW 0XC0   
    SUBWF TMPA,W
    BTFSS STATUS,Z
    GOTO CONT
    
    MOVLW 0X7F	    ;WE ARE ON THE LAST PAGE
    SUBWF TMPB,W	
    BTFSC STATUS,Z
    GOTO LASTPAGE
    
CONT 
    MOVLW 0X47	    ;THE PC CAN START ANOTHER BYTELOAD
    MOVWF TXREG
    CALL WAITTX
    GOTO BYTELOAD

    
LASTPAGE
    
    MOVLW 0X47	    ;THE PC CAN START ANOTHER BYTELOAD BUT THIS IS THE LAST AND WE NEED TO RE-ENABLE THE WRITE PROTECTION BEFORE
    MOVWF TXREG
    CALL WAITTX
    
;	RE-ENABLE WRITE PROTECTION:
;------------LOAD AA TO 5555--------------
    MOVLW 0X55
    MOVWF PORTA
    MOVWF PORTB
    
    MOVLW 0XAA
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;------------------------------------------

;------------LOAD 55 TO 2AAA---------------
    MOVWF PORTA ;DEJA AVEM AA IN W
    MOVLW 0X2A
    MOVWF PORTB
    
    MOVLW 0X55
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;------------------------------------------

;----------LOAD A0 TO 5555-----------------
    MOVWF PORTA
    MOVWF PORTB  ;DEJA AVEM 55 IN W
    
    MOVLW 0XA0
    MOVWF PORTD
    
    BCF PORTE,RE0   ;WRITE ENABLE ON
    BCF PORTE,RE2   ;CHIP ENABLE ON
    BSF PORTE,RE2   ;CHIP ENABLE OFF		
    BSF PORTE,RE0   ;WRITE ENABLE OFF
;-----------------------------------------
    
    MOVF TMPA,W
    MOVWF PORTA
    MOVF TMPB,W
    MOVWF PORTB
    
    
LASTBLOAD		    ;SAME AS THE NORMAL BYTELOADS JUST THE LAST ONE
;    BTFSS PIR1,RCIF
;    GOTO LASTBLOAD
    
    MOVF RCREG,W
    MOVWF PORTD	 
    MOVWF PDATA
    
    NOP
    NOP
    BCF PORTE,RE0   ;WRITE ENABLE ON	   
					    
    BCF PORTE,RE2   ;CHIP ENABLE ON	   
					   
    BSF PORTE,RE2   ;CHIP ENABLE OFF	    
					    
    BSF PORTE,RE0   ;WRITE ENABLE OFF
    
    INCFSZ TMPA,F   ;TMPA OVERFLOW -> WE FINISHED THE LAST PAGE
    GOTO LASTBLOADCONT
    GOTO OVF
    
LASTBLOADCONT
    MOVLW 0X57	    
    MOVWF TXREG	   
    CALL WAITTX
    MOVF TMPA,W
    MOVWF PORTA
    GOTO LASTBLOAD
    
OVF
    MOVLW 0X47	    ;FINISHED LAST PAGE
    MOVWF TXREG
    CALL WAITTX
    
    DECF TMPA,W
    MOVWF PORTA
    
    BSF STATUS,RP0
    MOVLW 0XFF
    MOVWF TRISD	    ;PORTD INPUT FOR POLLING
    BCF STATUS,RP0
    
LASTPOLL
    BCF PORTE,RE2
    BCF PORTE,RE1
    NOP
    NOP
    MOVF PORTD,W
    BSF PORTE,RE1
    BSF PORTE,RE2
    SUBWF PDATA,W
    BTFSS STATUS,Z
    GOTO LASTPOLL
    
    MOVLW 0X47
    MOVWF TXREG
    CALL WAITTX	    ;FINISHED PAGE WRITE (THE LAST PAGE)
    
    RETURN

READ
    MOVLW 0X41	 ;-|
    MOVWF TXREG	 ;-| SEND 'A' FOR ACK
    CALL WAITTX
    
    CLRF PORTA
    CLRF PORTB
    CLRF TMPA
    CLRF TMPB
    
    BSF STATUS,RP0
    MOVLW 0XFF
    MOVWF TRISD	    ;PORTD INPUT FOR READING
    BCF STATUS,RP0
    
READLOOP
    BCF PORTE,RE2  ;CHIP ENABLE ON
    
    BCF PORTE,RE1  ;OUTPUT ENABLE ON
    
    MOVF PORTD,W    ;CITESTE ADDRESS BUS
    
    BSF PORTE,RE1   ;OUTPUT ENABLE OFF
    
    BSF PORTE,RE2   ;CHIP ENABLE OFF
    
    MOVWF TXREG	    ;SEND THE BYTE WE READ THROUGH UART
    CALL WAITTX
    
    INCFSZ TMPA,F   
    GOTO SKIPOVFR   
    INCF TMPB,F	    
SKIPOVFR	    
    MOVF TMPA,W	    
    MOVWF PORTA	    
    MOVF TMPB,W	   
    MOVWF PORTB	   
     
    BTFSS TMPB,7
    GOTO READLOOP
    
    RETURN
    
    END